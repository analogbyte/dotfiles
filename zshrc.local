# autostart tmux
if which tmux 2>&1 >/dev/null; then
    test -z "$TMUX" && (tmux attach || tmux new-session)
fi

# basic stuff
setopt vi
bindkey '\e.' insert-last-word # make alt+dot work in vim mode
export TERM="screen-256color"

# virtualenvs
export WORKON_HOME=~/.envs

if which apt-get 2>&1 >/dev/null; then
    alias ack=ack-grep
    if [ -e "/usr/local/bin/virtualenvwrapper.sh" ]; then
        source /usr/local/bin/virtualenvwrapper.sh
    fi
    if [ -e "/etc/bash_completion.d/virtualenvwrapper" ]; then
        source /etc/bash_completion.d/virtualenvwrapper
    fi
fi

if which pacman 2>&1 >/dev/null; then
    if [ -e "/usr/bin/virtualenvwrapper.sh" ]; then
        source /usr/bin/virtualenvwrapper.sh
    fi
fi

# use the vi navigation keys (hjkl) besides cursor keys in menu completion
bindkey -M menuselect 'h' vi-backward-char        # left
bindkey -M menuselect 'k' vi-up-line-or-history   # up
bindkey -M menuselect 'l' vi-forward-char         # right
bindkey -M menuselect 'j' vi-down-line-or-history # bottom

# just type '...' to get '../..'
rationalise-dot() {
    local MATCH
    if [[ $LBUFFER =~ '(^|/| |     |'$'\n''|\||;|&)\.\.$' ]]; then
        LBUFFER+=/
        zle self-insert
        zle self-insert
    else
        zle self-insert
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

# without this, typing a . aborts incremental history search
bindkey -M isearch . self-insert

# shorter man page lines
export MANWIDTH=${MANWIDTH:-80}

# ssh wrapper that rename current tmux window to the hostname of the
# remote host. http://www.spatof.org/blog/tmux-ssh-rename-window.html
ssh() {
    if [[ $# == 0 || -z $TMUX ]]; then
        command ssh $@
        return
    fi
    # The hostname is the last parameter (i.e. ${(P)#})
    local remote=${(P)#}
    local old_name="$(tmux display-message -p '#W')"
    local renamed=0
    if [[ $remote != -* ]]; then
        renamed=1
        tmux rename-window $remote
    fi
    command ssh $@
    if [[ $renamed == 1 ]]; then
        tmux rename-window "$old_name"
    fi
}

DIRSTACKSIZE=8
setopt autopushd pushdminus pushdsilent pushdtohome

# aliases
alias dh='dirs -v'
alias -g L=' | less'
alias -g G=' | grep'
alias -g A=' | ack'
alias -g V=' | vim -'
alias t=task
