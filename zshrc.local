# autostart tmux
if which tmux 2>&1 >/dev/null; then
    test -z "$TMUX" && (tmux attach -t base || tmux new -s base)
fi

# basic stuff
setopt vi
bindkey '\e.' insert-last-word # make alt+dot work in vim mode

# virtualenvs
export WORKON_HOME=~/.envs

if which apt-get 2>&1 >/dev/null; then
    if [ -e "/usr/local/bin/virtualenvwrapper.sh" ]; then
        source /usr/local/bin/virtualenvwrapper.sh
    fi
    if [ -e "/etc/bash_completion.d/virtualenvwrapper" ]; then
        source /etc/bash_completion.d/virtualenvwrapper
    fi
fi

if which pacman 2>&1 >/dev/null; then
    if [ -e "/usr/bin/virtualenvwrapper.sh" ]; then
        source /usr/bin/virtualenvwrapper.sh
    fi
fi

# use the vi navigation keys (hjkl) besides cursor keys in menu completion
bindkey -M menuselect 'h' vi-backward-char        # left
bindkey -M menuselect 'k' vi-up-line-or-history   # up
bindkey -M menuselect 'l' vi-forward-char         # right
bindkey -M menuselect 'j' vi-down-line-or-history # bottom

# just type '...' to get '../..'
rationalise-dot() {
    local MATCH
    if [[ $LBUFFER =~ '(^|/| |     |'$'\n''|\||;|&)\.\.$' ]]; then
        LBUFFER+=/
        zle self-insert
        zle self-insert
    else
        zle self-insert
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

# without this, typing a . aborts incremental history search
bindkey -M isearch . self-insert

# shorter man page lines
export MANWIDTH=${MANWIDTH:-80}

# ssh/mosh wrapper that rename current tmux window to the hostname of the
# remote host
ssh() {
    if [[ $(tmux display-message -p '#W') = "local" ]]; then
        trap "tmux rename-window 'local'" INT PIPE EXIT
        test -n "$TMUX" && tmux rename-window "$(echo "${@: -1}" | cut -d '@' -f 2)"
        command ssh "$@"
        tmux rename-window -t:"$(echo "${@: -1}" | cut -d '@' -f 2)" 'local'
        trap - INT PIPE EXIT
    else
        command ssh "$@"
    fi
}
mosh() {
    if [[ $(tmux display-message -p '#W') = "local" ]]; then
        trap "tmux rename-window 'local'" INT PIPE EXIT
        test -n "$TMUX" && tmux rename-window "$(echo "${@: -1}" | cut -d '@' -f 2)"
        command mosh "$@"
        tmux rename-window -t:"$(echo "${@: -1}" | cut -d '@' -f 2)" 'local'
        trap - INT PIPE EXIT
    else
        command mosh "$@"
    fi
}

DIRSTACKSIZE=8
setopt autopushd pushdminus pushdsilent pushdtohome

# aliases
alias dh='dirs -v' # show dirstack
# global aliases
alias -g L=' | less'
alias -g G=' | grep'
alias -g A=' | ag'
alias -g V=' | vim -'
# convenience
alias t=task
alias tmuxlink="tmux link-window -k -s base:1 -t 1"

# change conflicting grml default
unalias ag
salias agu="apt-get upgrade"
